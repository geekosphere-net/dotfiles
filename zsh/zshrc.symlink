# Local customizations
[[ -f ~/.zshrc.local.pre ]] && source ~/.zshrc.local.pre

# Setup Oh-My-Zsh
: ${ZSH:=~/.oh-my-zsh}
: ${ZSH_THEME:="fishy"}
: ${ENABLE_CORRECTION:="true"}
# Plugin list â€” set plugins in ~/.zshrc.local.pre to override for this VM.
# Falls back to a minimal base set if not set there.
: ${plugins:=(git git-flow-avh zsh-syntax-highlighting sudo extract colored-man-pages)}
source $ZSH/oh-my-zsh.sh


# Load all of the ~/.dotfiles customizations

typeset -U config_files
config_files=(~/.dotfiles/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# dircolors
[[ -f ~/.dircolors ]] && eval "$(dircolors ~/.dircolors)"

umask 0022
#set bell-style none
[[ -n ${TMUX} ]] && export TERM="tmux-256color"

# Local customizations
[[ -f ~/.zshrc.local.post ]] && source ~/.zshrc.local.post

true

