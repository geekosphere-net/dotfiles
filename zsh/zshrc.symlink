# Local customizations
[[ -f ~/.zshrc.local ]] && source ~/.zshrc.local

# Setup Oh-My-Zsh
: ${ZSH:=~/.oh-my-zsh}
: ${ZSH_THEME:="fishy"}
: ${ENABLE_CORRECTION:="true"}
plugins=(git git-flow-avh docker docker-compose zsh-completions zsh-autosuggestions zsh-navigation-tools)
source $ZSH/oh-my-zsh.sh


# Load all of the ~/.dotfiles customizations

typeset -U config_files
config_files=(~/.dotfiles/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit && compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# dircolors
[[ -f ~/.dircolors ]] && eval `dircolors ~/.dircolors`

umask 0022

